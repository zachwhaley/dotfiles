#!/bin/zsh
#
# bootstrap installs things.

cd "$(dirname "$0")"
DOTFILES_ROOT=$(pwd)

set -e
set +o noclobber

echo ''

info () {
  printf "  [ \033[00;34m..\033[0m ] $1"
}

user () {
  printf "\r  [ \033[0;33m?\033[0m ] $1 "
}

success () {
  printf "\r\033[2K  [ \033[00;32mOK\033[0m ] $1\n"
}

fail () {
  printf "\r\033[2K  [\033[0;31mFAIL\033[0m] $1\n"
  echo ''
  exit
}

link_file () {
  local src=$1 dst=$2

  local overwrite= backup= skip=
  local action=

  if [ -f "$dst" -o -d "$dst" -o -L "$dst" ]
  then
    if [[ "$overwrite_all" == "false"  &&  "$backup_all" == "false"  &&  "$skip_all" == "false" ]]
    then
      local currentSrc="$(readlink $dst)"
      if [[ "$currentSrc" == "$src" ]]
      then
        skip=true;
      else
        user "File already exists: $(basename "$src"), what do you want to do? \
[s]kip, [S]kip all, [o]verwrite, [O]verwrite all, [b]ackup, [B]ackup all?"
        read -rs -k 1 action

        case "$action" in
          o )
            overwrite=true;;
          O )
            overwrite_all=true;;
          b )
            backup=true;;
          B )
            backup_all=true;;
          s )
            skip=true;;
          S )
            skip_all=true;;
          * )
            ;;
        esac
      fi
    fi

    overwrite=${overwrite:-$overwrite_all}
    backup=${backup:-$backup_all}
    skip=${skip:-$skip_all}

    if [[ "$overwrite" == "true" ]]
    then
      rm -rf "$dst"
      success "removed $dst"
    fi

    if [[ "$backup" == "true" ]]
    then
      mv "$dst" "${dst}.backup"
      success "moved $dst to ${dst}.backup"
    fi

    if [[ "$skip" == "true" ]]
    then
      success "skipped $src"
    fi
  fi

  if [[ "$skip" != "true" ]]  # "false" or empty
  then
    ln -s "$src" "$dst"
    success "linked $src to $dst"
  fi
}

install_dotfiles () {
  info 'installing dotfiles'

  local overwrite_all=false backup_all=false skip_all=false

  for src in $(find "$DOTFILES_ROOT" -maxdepth 2 -name '*.symlink')
  do
    dst="$HOME/.$(basename "${src%.*}")"
    link_file "$src" "$dst"
  done
}

setup_zsh () {
  info 'installing Zsh files'

  local overwrite_all=false backup_all=false skip_all=false

  # Mostly from Prezto README on installation
  setopt EXTENDED_GLOB
  for src in "${ZDOTDIR:-$HOME}"/.zprezto/runcoms/^README.md(.N)
  do
    link_file "$src" "${ZDOTDIR:-$HOME}/.${src:t}"
  done
  if [[ ! "$(getent passwd $LOGNAME | cut -d: -f7)" =~ ".*zsh" ]]
  then
    chsh -s /bin/zsh
  fi
}

install_dotfiles
setup_zsh

echo ''
echo '  All installed!'
